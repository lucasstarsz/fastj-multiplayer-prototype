plugins {
    id 'java'
    id 'application'
    id 'org.beryx.jlink' version '2.24.4'
}

group('io.github.lucasstarsz')
version('0.0.1')
description('WIP | A game of some sort. | WIP')

application.getMainClass().set('game.ClientMain')
application.mainModule.set('tech.fastj.multiplayer.game')

sourceSets {
    main.java.srcDirs = ['main/java']
    main.resources.srcDirs = ['main/resources']
    test.java.srcDirs = ['test/java']
    test.resources.srcDirs = ['test/resources']
}


repositories.maven { url('https://jitpack.io/') }
repositories.mavenCentral()
dependencies.implementation(libs.bundles.fastj)
dependencies.implementation(projects.networking)
dependencies.implementation(projects.core)


jlink {

    options.addAll(
            '--strip-debug',
            '--no-header-files',
            '--no-man-pages',
            '--compress', '1',
    )

    launcher {
        name = 'Snowball Fight'
        noConsole = false
    }

    forceMerge('slf4j-api', 'slf4j-simple')

    jpackageImage {
        doLast {
            copy {
                from("music") {
                    include "*.*"
                }
                into "$buildDir/jpackage/Snowball Fight/music"
            }
        }
    }

    jpackage {

        def iconPath = '../project-resources/fastj_icon'
        def currentOs = org.gradle.internal.os.OperatingSystem.current()

        addExtraDependencies("slf4j")

        installerOptions += [
                '--name', 'Snowball Fight',
                '--description', project.description,
                '--vendor', project.group,
                '--app-version', project.version,
                '--license-file', "$rootDir/LICENSE.md",
                '--copyright', 'Copyright (c) 2022 Andrew Dey',
                '--vendor', 'Andrew Dey'
        ]


        if (currentOs.windows) {
            installerType = 'msi'
            icon = "${iconPath}.ico"
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-shortcut']
        } else if (currentOs.linux) {
            installerType = 'deb'
            icon = "${iconPath}.png"
            installerOptions += ['--linux-shortcut']
        } else if (currentOs.macOsX) {
            installerType = 'pkg'
            icon = "${iconPath}.icns"
            installerOptions += ['--mac-package-name', project.name]
        }
    }
}

